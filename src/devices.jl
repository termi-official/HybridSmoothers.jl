import KernelAbstractions as KA

abstract type AbstractDevice{ValueType, IndexType} end
abstract type AbstractCPUDevice{ValueType, IndexType} <: AbstractDevice{ValueType, IndexType} end
abstract type AbstractGPUDevice{ValueType, IndexType} <: AbstractDevice{ValueType, IndexType} end

value_type(::AbstractDevice{ValueType}) where {ValueType} = ValueType
index_type(::AbstractDevice{<:Any, IndexType}) where {IndexType} = IndexType


"""
    SequentialCPUDevice

Sequential algorithms on CPU.
"""
struct SequentialCPUDevice{ValueType, IndexType} <: AbstractCPUDevice{ValueType, IndexType} end
SequentialCPUDevice() = SequentialCPUDevice{Float64, Int64}()


"""
    PolyesterDevice

Threaded algorithms via Polyester.jl .
"""
struct PolyesterDevice{ValueType, IndexType} <: AbstractCPUDevice{ValueType, IndexType}
    chunksize::IndexType
end
PolyesterDevice() = PolyesterDevice{Float64, Int64}(32)
PolyesterDevice(i::Int) = PolyesterDevice{Float64, Int64}(i)


"""
    CudaDevice

Please add CUDA.jl to your Project to make this device work.
"""
struct CudaDevice{ValueType, IndexType} <: AbstractGPUDevice{ValueType, IndexType}
    threads::Union{IndexType, Nothing}
    blocks::Union{IndexType, Nothing}
end

CudaDevice() = CudaDevice{Float32, Int32}(nothing, nothing)
CudaDevice(threads::IndexType, blocks::IndexType) where {IndexType} =
    CudaDevice{Float32, IndexType}(threads, blocks)

# KA compat
default_backend(::SequentialCPUDevice) = KA.CPU()
default_backend(::PolyesterDevice) = KA.CPU()
# default_backend(::CudaDevice) is provided by the CUDA extension.
